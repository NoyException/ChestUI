plugins {
    id 'java'
    id 'maven-publish'
    id("com.diffplug.spotless") version "6.25.0"
}

group = 'cn.noy'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    google()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

spotless {
    java {
        eclipse()
    }
}

tasks.register('setupServer', Exec) {
    workingDir file('./devtools')
    executable 'bash'
    args './setup-server.sh'
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register('runServer', Exec) {
    dependsOn setupServer
    workingDir file('./server')
    executable 'bash'
    args './run.sh'
    standardOutput = System.out
    errorOutput = System.err
}

tasks.register('copyJarToServerPlugins', Copy) {
    dependsOn tasks.setupServer
    from tasks.jar
    into file('./server/plugins')
}

tasks.register('buildAndCopy') {
    dependsOn tasks.build
    finalizedBy tasks.copyJarToServerPlugins
}

tasks.register('bac') {
    dependsOn tasks.buildAndCopy
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

tasks.build.dependsOn('spotlessApply')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            groupId = 'cn.noy'
            artifactId = 'chest-ui'

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/NoyException/ChestUI")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}